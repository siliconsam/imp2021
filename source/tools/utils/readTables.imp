%begin

    %constinteger max grammar  = 1720
    %constinteger max kdict    = 618

    !*** start of generated tables ***
    %include "i77.tables.imp"
    !***  end  of generated tables ***

    %integer max symbol length

    %routine showsymbol( %shortinteger ch )
        %if ch >= 32 %start
            printstring("'")
            printsymbol(ch)
            printstring("'")
        %finish %else %start
            printstring(itos(ch,2))
        %finish

    %end

    %routine dumpText
        %integer i
        %integer nlflag
        %integer value
        %integer flag,o1,ss,o2,t,link

        max symbol length = 0
        %for i = 0, 1, 255 %cycle
            %if length(text(i)) > max symbol length %start
                max symbol length = length(text(i))
            %finish
        %repeat

        nlflag = 1
        printstring("Start dumpText"); newline
        %for i = 0, 1, 255 %cycle
            nlflag = 1
            printstring("text[".itos(i,3)."]== ")
            spaces( max symbol length - length(text(i)));
            printstring(text(i))

            %if (0 <= i <= 119) %start
                value = initial(i)

                flag = (value >> 15) & 1
                o1 = (value >> 14) & 1
                ss = (value >> 12) & 3
                o2 = (value >> 9) & 7
                t = (value >> 8) & 1
                link = value & 255

                space;
                %if (flag = 1) %then printstring("flag ") %else spaces(5)
                %if (o1 = 1) %then printstring("o1 ") %else spaces(3)
                printstring("ss=".itos(ss,0)." ")
                printstring("o2=".itos(o2,0)." ")
                %if (t = 1) %then printstring("t ") %else spaces(2)
                printstring("link=".itos(link,0))

            %finish %else %if (130 <= i <= 179) %start
                printstring(" == ");printstring(text(atomic(i)))
            %finish %else %if (200 <= i <= 255) %start
                printstring(" @ ".itos(phrase(i),0))
            %finish

            newline

        %repeat
        newline

    %end

    %routine dumpGram
        %integer i
        %integer value
        %integer more,o1,order,type,class

        printstring("Start dumpGram"); newline
        %for i = 0, 1, gmax1 %cycle
            value = gram(i)

            more = (value >> 15) & 1
            o1 = (value >> 14) & 1
            order = (value >> 12) & 3
            type = (value >> 8) & 15
            class = value & 255

            printstring("gram[".itos(i,3)."]== ")
            %if more = 1 %then printstring("more ") %else spaces(5)
            %if o1 = 1 %then printstring("o1 ") %else spaces(3)
            printstring("order=");printstring(itos(order,0));printstring(" ")
            printstring("type=");printstring(itos(type,0));printstring(" ")
            spaces( max symbol length - length(text(class)));printstring(text(class))
            space; printstring(itos(glink(i),4))
            newline
        %repeat
        newline

    %end

    %routine dumpkdict
        %integer i
        %integer value
        %shortinteger ch1,ch2,flag

        printstring("Start dumpkdict"); newline
        %for i = 32, 1,max kdict %cycle
            value = kdict(i)

            printstring("kdict[".itos(i,3)."] = ")

            %if value&16_8000 # 0 %start
                printstring(" F ")
            %else
                spaces(3)
            %finish

            %if value&16_4000 # 0 %start
                printstring(" E ")
            %else
                spaces(3)
            %finish
			
            ch1 = value & 16_7f
            ch2 = (value >> 7) & 16_7f

            printstring(" ch2=")
            showsymbol( ch2 )

            printstring(" ch1=")
            showsymbol( ch1 )

            newline

        %repeat
        newline

    %end

    dumpText
    dumpGram
    dumpkdict

%endofprogram
